//let PI: float64 = 3.141592653589793;
let SOLAR_MASS: float64 = 4.0 * PI * PI;
let DAYS_PER_YEAR: float64 = 365.24;

class Body {
    public x: float64;
    public y: float64;
    public z: float64;
    public vx: float64;
    public vy: float64;
    public vz: float64;
    public mass: float64;

    constructor(
        x: float64,
        y: float64,
        z: float64,
        vx: float64,
        vy: float64,
        vz: float64,
        mass: float64
    ) {
        this.x = x;
        this.y = y;
        this.z = z;
        this.vx = vx;
        this.vy = vy;
        this.vz = vz;
        this.mass = mass;
    }

    offsetMomentum(px: float64, py: float64, pz: float64): Body {
        this.vx = -px / SOLAR_MASS;
        this.vy = -py / SOLAR_MASS;
        this.vz = -pz / SOLAR_MASS;
        return this;
    }
}

function Jupiter(): Body {
    return new Body(
        4.84143144246472090,
        -1.16032004402742839,
        -0.103622044471123109,
        0.00166007664274403694 * DAYS_PER_YEAR,
        0.00769901118419740425 * DAYS_PER_YEAR,
        -0.0000690460016972063 * DAYS_PER_YEAR,
        0.00095479193842432661 * SOLAR_MASS
    );
}

function Saturn(): Body {
    return new Body(
        8.34336671824457987,
        4.12479856412430479,
        -0.403523417114321381,
        -0.002767425107268624 * DAYS_PER_YEAR,
        0.0049985280123491724 * DAYS_PER_YEAR,
        0.0000230417297573764 * DAYS_PER_YEAR,
        0.0002858859806661308 * SOLAR_MASS
    );
}

function Uranus(): Body {
    return new Body(
        12.8943695621391310,
        -15.1111514016986312,
        -0.223307578892655734,
        0.0029646013756476162 * DAYS_PER_YEAR,
        0.0023784717395948095 * DAYS_PER_YEAR,
        -0.000029658956854024 * DAYS_PER_YEAR,
        0.0000436624404335156 * SOLAR_MASS
    );
}

function Neptune(): Body {
    return new Body(
        15.3796971148509165,
        -25.9193146099879641,
        0.179258772950371181,
        0.002680677724903893 * DAYS_PER_YEAR,
        0.001628241700382423 * DAYS_PER_YEAR,
        -0.000095159225451972 * DAYS_PER_YEAR,
        0.0000515138902046612 * SOLAR_MASS
    );
}

function Sun(): Body {
    return new Body(
        0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SOLAR_MASS
    );
}

class NBodySystem {
    public bodies: Array< Body >;
    constructor(bodies: Array< Body >) {
        let px: float64 = 0.0;
        let py: float64 = 0.0;
        let pz: float64 = 0.0;
        let size: int32 = bodies.length;
        let i: int32 = 0;
        let b: Body;
        let m: float64;

        while (i < size) {
            b = bodies[i];

            m = b.mass;
            px = px + b.vx * m;
            py = py + b.vy * m;
            pz = pz + b.vz * m;

            i++;
        }
        this.bodies = bodies;
        this.bodies[0].offsetMomentum(px, py, pz);
    }

    advance(dt: float64): void {
        let dx: float64,
            dy: float64,
            dz: float64,
            ix: float64,
            iy: float64,
            iz: float64,
            bivx: float64,
            bivy: float64,
            bivz: float64,
            distance: float64,
            distanceSq: float64,
            mag: float64,
            bim: float64,
            bjm: float64,
            bodyim: float64;

        let bodyi: Body, bodyj: Body;
        let bodies: Array< Body > = this.bodies;
        let size: int32 = bodies.length;
        let i: int32 = 0, j: int32 = 0;

        while (i < size) {
            bodyi = bodies[i];

            ix = bodyi.x;
            iy = bodyi.y;
            iz = bodyi.z;

            bivx = bodyi.vx;
            bivy = bodyi.vy;
            bivz = bodyi.vz;

            bodyim = bodyi.mass;
            j = i + 1;

            while (j < size) {
                bodyj = bodies[j];
                dx = ix - bodyj.x;
                dy = iy - bodyj.y;
                dz = iz - bodyj.z;

                distanceSq = dx * dx + dy * dy + dz * dz;
                distance = sqrt64(distanceSq);
                mag = dt / (distanceSq * distance);

                bim = bodyim * mag;
                bjm = bodyj.mass * mag;

                bivx = bivx - dx * bjm;
                bivy = bivy - dy * bjm;
                bivz = bivz - dz * bjm;

                bodyj.vx = bodyj.vx + dx * bim;
                bodyj.vy = bodyj.vy + dy * bim;
                bodyj.vz = bodyj.vz + dz * bim;

                j++;
            }

            bodyi.vx = bivx;
            bodyi.vy = bivy;
            bodyi.vz = bivz;

            bodyi.x = bodyi.x + dt * bivx;
            bodyi.y = bodyi.y + dt * bivy;
            bodyi.z = bodyi.z + dt * bivz;

            i++;
        }
    }

    energy(): float64 {
        let dx: float64, dy: float64, dz: float64, distance: float64;
        let ix: float64, iy: float64, iz: float64, vx: float64, vy: float64, vz: float64, bim: float64;
        let e : float64 = 0.0;
        let bodies: Array< Body > = this.bodies;
        let size: int32 = bodies.length;
        let bodyi: Body, bodyj: Body;
        let i: int32 = 0,
            j: int32 = 0;

        while (i < size) {
            bodyi = bodies[i];

            ix = bodyi.x;
            iy = bodyi.y;
            iz = bodyi.z;

            vx = bodyi.vx;
            vy = bodyi.vy;
            vz = bodyi.vz;

            bim = bodyi.mass;

            e = e + 0.5 * bim * (vx * vx + vy * vy + vz * vz);
            j = i + 1;

            while (j < size) {
                bodyj = bodies[j];
                dx = ix - bodyj.x;
                dy = iy - bodyj.y;
                dz = iz - bodyj.z;

                distance = sqrt64(dx * dx + dy * dy + dz * dz);
                e = e - bim * bodyj.mass / distance;
                j++;
            }
            i++;
        }
        return e;
    }
}

export function test(n: int32): float64 {
    let array = new Array< Body >(5);

    array[0] = Sun();
    array[1] = Jupiter();
    array[2] = Saturn();
    array[3] = Uranus();
    array[4] = Neptune();

    let bodies: NBodySystem = new NBodySystem(array);
    let i: int32 = 0;

    while (i < n) {
        bodies.advance(0.01);
        i++;
    }
    return bodies.energy();
}

/*
let n: int32 = 500000;

console.time('t');
let bodies: NBodySystem = new NBodySystem(new Array< Body >(Sun(), Jupiter(), Saturn(), Uranus(), Neptune()));

console.log(bodies.energy().toFixed(9));
for (let i: int32 = 0; i < n; i++) {
    bodies.advance(0.01);
}
console.log(bodies.energy().toFixed(9));

// -0.169075164
// -0.169096567

console.timeEnd('t');
*/
